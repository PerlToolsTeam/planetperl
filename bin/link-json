#!/usr/bin/env perl

use strict;
use warnings;
use feature 'say';

use URI;
use XML::Feed;
use JSON::PP;

# Skip things that mess up the feed
my @skip = qw[
  https://stackoverflow.com/
  https://github.com/Perl/
  https://theweeklychallenge.org/tags 
  https://www.reddit.com/
  http://www.rabbitfarm.com/
];

my @title_skip = (
  'perl tokens'
);

my $feed_file = shift;
die "No feed given\n" unless $feed_file;

my $feed;

if ($feed_file =~ m[^https?://]) {
  $feed = XML::Feed->parse(URI->new($feed_file));
} else {
  $feed = XML::Feed->parse($feed_file);
}

my @links;

ENTRY:
for ($feed->entries) {
  for my $skip (@skip) {
    next ENTRY if $_->link =~ /^$skip/;
  }
  for my $skip (@title_skip) {
    next ENTRY if $_->title =~ /$skip/i;
  }
  push @links, {
    title  => $_->title,
    url    => $_->link,
    posted => ($_->modified // $_->issued)->iso8601,
  };
}

say JSON::PP->new->pretty->encode(\@links);
